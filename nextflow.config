nextflow.enable.dsl=2

// Default parameters that can be overridden
params {
    // Show help message
    help = false
    // Alternative workflow for initialisation only
    init = false
    // Alternative workflow for getting versions of pipeline and tools
    version = false

    // Optional annotation module
    annotation = false 

    // Default directory for input reads
    reads = "$projectDir/input"
    // Default output directory
    output = "$projectDir/output"
    // Default file publish mode
    file_publish = "copy"
    
    // Default databases directory for saving all the required databases
    db = "$projectDir/databases"

    // Default assembler
    assembler = "shovill"
    // Default assembler thread count (0 means all)
    assembler_thread = 0
    // Default minimum contig length
    min_contig_length = 500

    // Default link for SeroBA repository, and KMC kmer size for SeroBA 
    seroba_db_remote = "https://github.com/GlobalPneumoSeq/seroba/archive/refs/tags/v2.0.4.tar.gz"
    seroba_kmer = 71

    // Default link for Kraken2 Database, and usage of memory mapping
    kraken2_db_remote = "https://genome-idx.s3.amazonaws.com/kraken/minikraken2_v1_8GB_201904.tgz"
    kraken2_memory_mapping = true

    // Default referece genome assembly path for its BWA database 
    ref_genome = "$projectDir/data/ATCC_700669_v1.fa"

    // Default links for PopPUNK Database and External Clusters
    poppunk_db_remote = "https://gps-project.cog.sanger.ac.uk/GPS_v10.tar.gz"
    poppunk_ext_remote = "https://gps-project.cog.sanger.ac.uk/GPS_v10_external_clusters.csv"

    // Default database for Bakta
    bakta_db_remote = "https://zenodo.org/records/14916843/files/db-light.tar.xz"

    // Default values for QC
    spneumo_percentage = 60.00
    non_strep_percentage = 2.00
    ref_coverage = 60.00
    het_snp_site = 220
    contigs = 500
    length_low = 1900000
    length_high = 2300000
    depth = 20.00

    // Default ARIBA referece sequences and metadata paths
    ariba_ref = "$projectDir/data/ariba_ref_sequences.fasta"
    ariba_metadata = "$projectDir/data/ariba_metadata.tsv"

    // Default resistance phenotypes to MIC (minimum inhibitory concentration) lookup table
    resistance_to_mic = "$projectDir/data/resistance_to_MIC.tsv"

    // Toggle for removing .bam and .sam files mid-run to reduce storage requirement
    // Warning: This will break the -resume function of Nextflow  
    lite = false
}

// Set auto-retry and process container images
process {
    maxRetries = 2
    errorStrategy = { task.attempt <= process.maxRetries ? 'retry' : 'ignore' }

    withLabel: bash_container {
        container = 'wbitt/network-multitool:69aa4d5'
    }
    withLabel: python_container {
        container = 'amancevice/pandas:2.0.2'
    }
    withLabel: fastp_container {
        container = 'staphb/fastp:0.23.4'
    }
    withLabel: unicycler_container {
        container = 'staphb/unicycler:0.5.0'
    }
    withLabel: shovill_container {
        container = 'staphb/shovill:1.1.0-2022Dec'
    }
    withLabel: quast_container {
        container = 'staphb/quast:5.0.2'
    }
    withLabel: bwa_container {
        container = 'staphb/bwa:0.7.17'
    }
    withLabel: samtools_container {
        container = 'staphb/samtools:1.16'
    }
    withLabel: bcftools_container {
        container = 'staphb/bcftools:1.16'
    }
    withLabel: poppunk_container {
        container = 'staphb/poppunk:2.6.3'
    }
    withLabel: spn_pbp_amr_container {
        container = 'sangerbentleygroup/spn-pbp-amr:23.10.2'
    }
    withLabel: ariba_container {
        container = 'staphb/ariba:2.14.6'
    }
    withLabel: mlst_container {
        container = 'staphb/mlst:2.23.0-2024-07-01'
    }
    withLabel: kraken2_container {
        container = 'staphb/kraken2:2.1.2-no-db'
    }
    withLabel: seroba_container {
        container = 'sangerbentleygroup/seroba:2.0.4'
    }
    withLabel: bakta_container {
        container = 'staphb/bakta:1.11.0'
    }
}

// Set parameters of different run environments
profiles {

    // Default Profile for local machine
    // Docker as container engine, execute by local machine
    standard {
        process{
            executor = 'local'
        }
        docker {
            enabled = true
            runOptions = '-u $(id -u):$(id -g)'
        }
    }

    // Alternative Profile for local machine
    // Singularity as container engine, execute by local machine
    singularity {
        params.singularity_cachedir = "$projectDir/singularity_cache"

        singularity {
            enabled = true
            autoMounts = true
            cacheDir = params.singularity_cachedir
        }
    }

    // Profile for LSF, tested on Sanger farm22
    // Singularity as container engine, execute by LSF
    lsf {
        params.singularity_cachedir = "$projectDir/singularity_cache"
        params.kraken2_memory_mapping = false
        params.bakta_db_remote = "https://zenodo.org/records/14916843/files/db.tar.xz"

        process {
            executor = 'lsf'
            scratch = true
            time = {30.min * task.attempt}
            maxRetries = 4

            withLabel: farm_low {
                cpus = 1
                memory = {1.GB * task.attempt}
            }
            withLabel: farm_mid {
                cpus = 8
                memory = {4.GB * task.attempt}
            }
            withLabel: farm_high {
                cpus = 32
                memory = {16.GB * task.attempt}
            }
            withLabel: farm_slow {
                time = {2.hour * task.attempt}
            }
            withLabel: farm_scratchless {
                scratch = false
            }
            withLabel: farm_local {
                executor = 'local'
            }
        }
        executor {
            perJobMemLimit = true
        }
        singularity {
            enabled = true
            autoMounts = true
            cacheDir = params.singularity_cachedir
        }
    }
    sangertower {
        tower {
            enabled = true
            endpoint = 'https://tower.internal.sanger.ac.uk/api/'
        }
    }

}

manifest {
    name = "GPS Pipeline"
    version = "1.1.0"
    contributors = [
        [
            name: "Harry C. H. Hung",
            affiliation: "Wellcome Sanger Institute",
            email: "ch31@sanger.ac.uk",
            github: "@HarryHung",
            contribution: "author",
            orcid: "0009-0005-3857-5242"
        ]
    ]
    homePage = "https://github.com/GlobalPneumoSeq/gps-pipeline/"
    description = "Nextflow Pipeline for processing Streptococcus pneumoniae sequencing raw reads (FASTQ files) by the GPS Project (Global Pneumococcal Sequencing Project)"
    doi="https://doi.org/10.1101/2024.11.27.625679"
    docsUrl = "https://github.com/GlobalPneumoSeq/gps-pipeline/blob/master/README.md"
    mainScript = "main.nf"
    defaultBranch = "main"
    nextflowVersion = ">=24.10"
    license = "GPL-3.0"
}